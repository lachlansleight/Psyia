// We need to manually mark what our kernel names are
#pragma kernel EmitFunction

struct ParticleData {
	float3 pos;
	float3 velocity;
	float4 color;
	int isAlive;
	float age;
};

RWStructuredBuffer<ParticleData> outputBuffer;
ConsumeStructuredBuffer<uint> deadList;

float3 SpawnPosition;
float3 LastSpawnPosition;

float3 SpawnVelocity;
float SpawnVelocityScatter;
float SpawnInheritVelocity;

float Time;

//returns random from 0 to 1
float rand(float2 n) {
	return frac(sin(dot(n, float2(12.9898, 78.233))) * 43758.5453);
}

[numthreads(1,1,1)]
void EmitFunction() {
	uint newIndex = deadList.Consume();
	ParticleData CurrentParticle = outputBuffer[newIndex];

	CurrentParticle.age = 0;
	CurrentParticle.isAlive = 1;
	CurrentParticle.pos = lerp(LastSpawnPosition, SpawnPosition, rand(float2(newIndex - 250.125, newIndex + 250.125)));
	CurrentParticle.pos += float3(rand(Time), rand(Time + 135.332), rand(Time - 135.332)) * 0.01;

	float3 RandomDirection = float3(
		rand(float2(newIndex, newIndex)),
		rand(float2(newIndex + 1000.5 + Time, newIndex + 1000.5 - Time)),
		rand(float2(newIndex - 1000.5 - (Time * 0.3234), newIndex - 1000.5 + (Time * 0.23412)))
	);
	RandomDirection -= float3(0.5, 0.5, 0.5);
	RandomDirection = normalize(RandomDirection);

	float ScatterStrength = rand(float2(newIndex + 500.25 - (Time * 2.3422), newIndex - 500.25 + (Time * 0.112334))) * SpawnVelocityScatter;

	CurrentParticle.velocity = (RandomDirection * ScatterStrength) + (SpawnVelocity * SpawnInheritVelocity);

	outputBuffer[newIndex] = CurrentParticle;
}